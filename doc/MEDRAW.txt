.Drawing Utilities

Lbl DSCR {
    .Draws the screen
    .r1: CamX (0-255)
    .r2: CamY (0-255)
    .r3: CamZ (0-255)

    .Saving args and vars to stack
    SSRG()
    S-195->S
    r_1->{S+192}
    r_2->{S+193}
    r_3->{S+194}
    .S+0 through S+95 is property bytes for each tile on-screen
    .S+96 through S+191 is ground heights for each tile on-screen (w/o stumps)

    .Getting property bytes and ground heights for each tile
    For(A,0,7)
        For(B,0,11)
            A*12+B->C
            GPBA({S+192}+B,{S+193}+A)->{S+C}
            GHAT({S+192}+B,{S+193}+A)-({S+C}e5)->{S+C+96}
        End
    End

    .Drawing each tile
    For(A,0,7)
        For(B,0,11)
            DTIL({S+192}+B,{S+193}+A,
                B*2*2*2,A*2*2*2,
                {S+194})
        End
    End

    .Recovering the stack
    S+195->S
    RSRG()

    Return
}

Lbl DTIL {
    .Draws a tile
    .r1: WorldX
    .r2: WorldY
    .r3: ScreenX
    .r4: ScreenY
    .r5: CameraZ

    .Storing args and variables
    SSRG()

    .Finding terrain height and property byte for this tile
    r_3/2/2/2->G
    r_4/2/2/2->H
    H*12+G+12+S->G
    {G}->D
    {G+96}->A

    .Finding screen pos of top tile
    r_5-A*2+r_4->C

    .Rendering wall beneath tile
    If r_4!=56
        {G+108}->B
        De7*2+(De6)->H .wall type
        While B<A
            Pt-Mask(r_3,A-B*2+C,H+4*2*2*2*2+Pic1STIL)
            B+4->B
        End
    End

    .Rendering the tile atop the wall
    If De6
        .If water, render a water tile
        Pt-Mask(r_3,C,Pic1STIL+128)
    Else
        .If land, find the ground type and render it
        .(The and-mask finds the upper half-byte of the property byte)
        Pt-Mask(r_3,C,D and 240+Pic1STIL)

        .Rendering borders with surrounding tiles
        .(bottom)
        If r_4!=56?{G+108}<A
            HLine(C+7,r_3,r_3+7)
        End

        .(top)
        If r_4?{G+84}<A
            HLine(C,r_3,r_3+7)
        End

        .(left)
        If r_3?{G+95}<A
            VLine(r_3,C,C+7)
        End

        .(right)
        If r_3!=88?{G+97}<A
            VLine(r_3+7,C,C+7)
        End
    End

    .Render stumps/rocks if needed
    If De4
        Pt-Mask(r_3,C-2,Pic1STIL+144)
    ElseIf De5
        Pt-Mask(r_3,C,Pic1STIL+160)
    End

    .Then, we can draw the entities at that position if applicable
    GSEH()->G
    If G
        For(H,1,G)
            {-H*2+S}^r->I
            {I}->J
            nib{GDB1ERUD*2+J}^r->K
            Pt-Mask(
                r_3,
                r_5-{I+5}*2+2-K+r_4,
                J*2*2*2*2+Pic1SENT
            )
        End
    End

    .Recovering the stack
    RSRG()

    Return
}
