.Text Routines

Lbl DTEX {
    .Display a block of text
        .The text is stored contiguously in memory from a base pointer.
        .Null bytes (\0) represent page breaks, and just calling Disp on the
        .   page should be sufficient to display it.
        .A double null byte (basically a page starting with a null byte) marks
        .   the end of things to display.
    .r1: Pointer to text, relative to the archived strings file

    While STBY(r_1) .(ends if current string starts with \0)
        .G is column, H is row
        0->G->H

        .Displaying characters until we hit a page break
        While STBY(r_1)->I
            .Keep displaying characters until we hit a null byte
            Output(G,H,I>Char)
            r_1++

            .Moving cursor
            G++
            If G>15
                0->G
                H++
                If H>7
                    0->H
                End
            End

            .Pausing briefly (the text slowly flows onto the screen!)
            Pause 8
        End

        .Waiting for a release and press of the enter button
        PAUS()

        .Incrementing to the next page
        r_1++
    End

    Return
}

Lbl PAUS {
    .Pauses, waiting for a full enter press (none -> press -> none)
    .Clears the screen afterwards

    .Oddly, I found that I have to put these little pauses within the while
    .   loops, or else they don't seem to run.
    While ITAH()
        Pause 8
    End
    Repeat ITAH()
    End
    While ITAH()
        Pause 8
    End

    ClrHome

    Return
}

Lbl ITAH {
    .Is Text Advance Held
    .Returns if any key that can advance text is held
    getKey(1)
        ??getKey(2)
        ??getKey(3)
        ??getKey(4)
        ??getKey(15)
        ??getKey(48)
        ??getKey(54)
        ??getKey(9)
        Return
}

Lbl DERR {
    .Display error
    .r1: Error code

    .Displaying
    ClrHome
    If r_1=1
        "Failed to load world"->G
    ElseIf r_1=2
        "No save found"->G
    ElseIf r_1=3
        "Failed to make save"->G
    ElseIf r_1=4
        "Save/world file mismatch"->G
    End
    Text(4,4,G)

    .Pausing for further input
    PAUS()

    Return
}
