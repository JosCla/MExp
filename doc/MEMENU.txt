.MExp Menus

Lbl MMEN {
    .Main menu
    .Displays the main menu and allows the user to navigate it
    .r1: LSAV exit code (0 = save file present, 1 = no save file present)
    .Returns the menu choice:
    .   0: Continue
    .   1: New Game
    .   2: Exit

    .Allocating stack
    SSRG()
    r_1->A

    .MMEN Start
    Lbl MMENS

    .Displaying the menu
    .(rendering world if applicable)
    !If A
        DRPL()
        Rect(3,3,46,40)
        WRect(4,4,44,38)
        WRect(4,4,44,38)^r
    End
    
    .(text)
    Fix 5
    Fix 1
    Text(10,6,"MExp")
    Fix 0
    Text(14,16,"Continue")
    Text(14,24,"New Game")
    Text(14,32,"Exit")
    Fix 4

    .Getting user input
    0->G
    1->H
    0->I
    Repeat getKey(9)
        DGWP()
    
        !If I
            If getKey(1)?G<2
                G++
                1->H
            ElseIf getKey(4)?G
                G--
                1->H
            End
        Else
            I--
        End
    
        If H
            WRect(5,16,8,24)
            WRect(5,16,8,24)^r
            Pt-Mask(5,G*8+16,Pic1SENT+48)
            0->H
            15->I
        End
    End

    .(going back if we chose an invalid option)
    If A?G=0
        DERR(A)
        Goto MMENS
    End

    .Fixing stack
    RSRG()

    GReturn
}
