.Move Entity (single Tick)

Lbl MVET {
    .Moves entities through a single tick
    .r1: Number of actions to execute in the current queue frame
    .  The movements should be executed from high to low memory addresses
    .U: Number of movements pushed to stack
    .  New movements will be pushed below stack pointer, to be executed from
    .  high to low memory addresses
    .  (put in U instead of T to be consistent with MVEN)

    .Allocating stack
    S-10->S
    A->{S}^r
    B->{S+2}^r
    C->{S+4}^r
    D->{S+6}^r
    E->{S+8}^r

    .Storing the base pointer of actions to execute in D
    S+10->D

    .Storing num actions to execute in A
    r_1->A

    .Executing all actions (storing total actions pushed in C)
    0->C
    For(B,1,A)
        .Getting pointer to current action
        A-B*5+D->E

        .Getting pointer to entity in question
        GCEP({E},{E+1})->G

        .Performing movement
        MVEN(G,signed{E+2},signed{E+3},{E+4})
        C+U->C
        -U*5+S->S
    End

    .(Putting number of movements added into U)
    C->U

    .Recovering stack
    D-10->S
    {S}^r->A
    {S+2}^r->B
    {S+4}^r->C
    {S+6}^r->D
    {S+8}^r->E
    S+10->S

    .Moving newly added actions directly underneath stack pointer
    U*5->G
    S-G->H
    CPRV(H-10,H,G)

    .(return)
    Return
}
